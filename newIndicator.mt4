// This work is licensed under a Attribution-NonCommercial-ShareAlike 4.0 International (CC BY-NC-SA 4.0) https://creativecommons.org/licenses/by-nc-sa/4.0/
// Â© LuxAlgo

//@version=5
strategy("Machine Learning Moving Average Strategy [LuxAlgo]", overlay=true, margin_long=100, margin_short=100)

//---------------------------------------------------------------------------------------------------------------------}
//Settings
//---------------------------------------------------------------------------------------------------------------------{
window = input.int(100, minval = 0)
forecast = input.int(3)
sigma = input.float(0.01, step = 0.1, minval = 0)

mult = input.float(2, 'Multiplicative Factor', minval = 0)
src = input.source(close, 'Source')

//Style
upCss = input(color.new(#5b9cf6, 50), 'Upper Extremity', group = 'Style')
dnCss = input(color.new(#e91e63, 50), 'Lower Extremity', group = 'Style')

bullCss = input(#3179f5, 'Moving Average', inline = 'ma', group = 'Style')
bearCss = input(#e91e63, ''              , inline = 'ma', group = 'Style')

//---------------------------------------------------------------------------------------------------------------------}
//Functions
//---------------------------------------------------------------------------------------------------------------------{
rbf(x1, x2, l) => math.exp(-math.pow(x1 - x2, 2) / (2.0 * math.pow(l, 2)))

kernel_matrix(X1, X2, l) =>
    km = matrix.new<float>(X1.size(), X2.size())
    i = 0
    for x1 in X1
        j = 0
        for x2 in X2
            rbfVal = rbf(x1, x2, l)
            km.set(i, j, rbfVal)
            j += 1
        i += 1
    km

//---------------------------------------------------------------------------------------------------------------------}
//Kernel Setup
//---------------------------------------------------------------------------------------------------------------------{
var identity = matrix.new<int>(window, window, 0)
var array<float> K_row = na

if barstate.isfirst
    xtrain = array.new<int>(0)
    xtest = array.new<int>(0)

    for i = 0 to window - 1
        for j = 0 to window - 1
            identity.set(i, j, i == j ? 1 : 0)
        xtrain.push(i)

    for i = 0 to window + forecast - 1
        xtest.push(i)

    s = identity.mult(sigma * sigma)
    Ktrain = kernel_matrix(xtrain, xtrain, window).sum(s)
    K_inv = Ktrain.pinv()
    K_star = kernel_matrix(xtrain, xtest, window)
    K_row := K_star.transpose().mult(K_inv).row(window + forecast - 1)

//---------------------------------------------------------------------------------------------------------------------}
//Moving Average Calculation
//---------------------------------------------------------------------------------------------------------------------{
var os = 0

mean = ta.sma(src, window)
float out = na

if bar_index > window
    dotprod = 0.0
    for i = 0 to window - 1
        dotprod += K_row.get(i) * (src[window - 1 - i] - mean)
    out := dotprod + mean

mae = ta.sma(math.abs(src - out), window) * mult
upper = out + mae
lower = out - mae

os := close > upper and out > out[1] ? 1 : close < lower and out < out[1] ? 0 : os

//---------------------------------------------------------------------------------------------------------------------}
//Strategy Execution
//---------------------------------------------------------------------------------------------------------------------{
var inLong = false
var inShort = false

if os != os[1]  // Signal change
    if os == 1
        strategy.close("Short")
        strategy.entry("Long", strategy.long)
    else
        strategy.close("Long")
        strategy.entry("Short", strategy.short)